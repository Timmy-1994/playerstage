plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "org.flywaydb.enterprise.flyway" version "8.0.0-beta3"

}

group = 'com.playerstage'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	mybatisGenerator
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0'
	implementation 'net.sf.json-lib:json-lib:2.4:jdk15'
	implementation 'io.springfox:springfox-boot-starter:3.0.0'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	implementation 'org.mybatis.generator:mybatis-generator-core:1.4.0'
	implementation 'org.mybatis.dynamic-sql:mybatis-dynamic-sql:1.2.1'

	mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.4.0' // duplicate
	mybatisGenerator 'org.mariadb.jdbc:mariadb-java-client' // duplicate
	mybatisGenerator files('build/libs/playerstage-0.0.1-SNAPSHOT-plain.jar')

}

def jdbcDriver = "org.mariadb.jdbc.Driver"
def props = new Properties()
file("/src/main/resources/application.properties").withInputStream { props.load(it) }

test {
	useJUnitPlatform()
}

tasks.register("mbgenerator") {

	if (!file('build/libs/playerstage-0.0.1-SNAPSHOT-plain.jar').exists()) {
		dependsOn(jar)
	}

	doLast {
		ant.properties['connectionURL'] = props["spring.datasource.url"]
		ant.properties['userId'] = props["spring.datasource.username"]
		ant.properties['password'] = props["spring.datasource.password"]
		ant.properties['targetProject'] = projectDir.path
		ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
		ant.properties['modelPackage'] = "com.playerstage.playerstage.models"
		ant.properties['mapperPackage'] = "com.playerstage.playerstage.mappers"
		ant.properties['driverClass'] = jdbcDriver

		ant.taskdef(
				name: 'mbgenerator',
				classname: 'org.mybatis.generator.ant.GeneratorAntTask',
				classpath: configurations.mybatisGenerator.asPath
		)
		ant.mbgenerator(overwrite:true,configfile: file("src/main/resources/mybatisGeneratorConfig.xml")) {
			propertyset {
				propertyref(name: 'connectionURL')
				propertyref(name: 'userId')
				propertyref(name: 'password')
				propertyref(name: 'driverClass')
				propertyref(name: 'targetProject')
				propertyref(name: 'src_main_java')
				propertyref(name: 'modelPackage')
				propertyref(name: 'mapperPackage')
			}
		}
	}
}

flyway {
	url = props["spring.datasource.url"]
    user = props["spring.datasource.username"]
    password = props["spring.datasource.password"]
	locations = [
		"filesystem:/database/migration"
	]
}